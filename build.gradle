plugins {
	id 'groovy'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id "org.asciidoctor.jvm.convert" version "3.3.2"
	id "org.asciidoctor.jvm.pdf" version "3.3.2"
}

group = 'it.gpa-group'
version = '1.0-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

configurations {
	all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }
}

dependencies {
	implementation 'org.apache.groovy:groovy'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	developmentOnly("org.springframework.boot:spring-boot-devtools")
}

tasks.withType(GroovyCompile).configureEach {
	groovyOptions.parameters = true
}

tasks.named('test') {
	useJUnitPlatform()
}

compileGroovy.finalizedBy(asciidoctor)
asciidoctor {
	baseDirFollowsSourceDir()
	attributes = [
			'revnumber': project.version,
			'source-highlighter': 'coderay',
			'icons': 'font',
			'toc': 'left',
			'toclevels': 3,
	]
	outputOptions {
		backends = ['html5']
	}
	doLast {
		copy {
			from('build/docs/asciidoc/')
			into "${projectDir}/docs/"
		}
	}
}

bootRun {
	jvmArgs("-Xms1g", "-Xmx1g")
}
